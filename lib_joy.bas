SHARED CONST JOY_UP      = %00000001
SHARED CONST JOY_DOWN    = %00000010
SHARED CONST JOY_LEFT    = %00000100
SHARED CONST JOY_RIGHT   = %00001000
SHARED CONST JOY_FIRE    = %00010000
SHARED CONST JOY_ANY_DIR = %00001111
SHARED CONST JOY_ANY     = %00011111

TYPE Joystick
    Value AS BYTE
    Prev AS BYTE
    Addr AS WORD

    SUB Init(Addr AS WORD) STATIC
        THIS.Addr = Addr
        THIS.Prev = PEEK(THIS.Addr) AND JOY_ANY
        THIS.Value = THIS.Prev
    END SUB

    SUB Update() STATIC
        THIS.Prev = THIS.Value    
        THIS.Value = PEEK(THIS.Addr) AND JOY_ANY
    END SUB

    FUNCTION North AS BYTE() STATIC
        RETURN (THIS.Value AND JOY_UP) = 0
    END FUNCTION

    FUNCTION South AS BYTE() STATIC
        RETURN (THIS.Value AND JOY_DOWN) = 0
    END FUNCTION

    FUNCTION East AS BYTE() STATIC
        RETURN (THIS.Value AND JOY_RIGHT) = 0
    END FUNCTION

    FUNCTION West AS BYTE() STATIC
        RETURN (THIS.Value AND JOY_LEFT) = 0
    END FUNCTION

    FUNCTION Button AS BYTE() STATIC
        RETURN (THIS.Value AND JOY_FIRE) = 0
    END FUNCTION

    FUNCTION ButtonOn AS BYTE() STATIC
        RETURN ((THIS.Value XOR THIS.Prev) AND JOY_FIRE) <> 0 AND THIS.Button()
    END FUNCTION

    FUNCTION ButtonOff AS BYTE() STATIC
        RETURN ((THIS.Value XOR THIS.Prev) AND JOY_FIRE) <> 0 AND NOT THIS.Button()
    END FUNCTION


    FUNCTION WaitSingleAction AS BYTE() STATIC
        DO
            CALL THIS.Update()
            ZP_B0 = THIS.Prev XOR THIS.Value
            IF ZP_B0 = JOY_FIRE AND THIS.Button() THEN RETURN JOY_FIRE
            IF ZP_B0 = JOY_UP AND THIS.North() THEN RETURN JOY_UP
            IF ZP_B0 = JOY_DOWN AND THIS.South() THEN RETURN JOY_DOWN
            IF ZP_B0 = JOY_LEFT AND THIS.West() THEN RETURN JOY_LEFT
            IF ZP_B0 = JOY_RIGHT AND THIS.East() THEN RETURN JOY_RIGHT
        LOOP
    END FUNCTION

    SUB WaitClick() STATIC
        CALL THIS.Update()
        DO UNTIL THIS.ButtonOn()
            CALL THIS.Update()
        LOOP
    END SUB

    FUNCTION XAxis AS INT() STATIC
        IF THIS.West() THEN RETURN -1
        IF THIS.East() THEN RETURN 1
        RETURN 0
    END FUNCTION

    FUNCTION YAxis AS INT() STATIC
        IF THIS.North() THEN RETURN -1
        IF THIS.South() THEN RETURN 1
        RETURN 0
    END FUNCTION
END TYPE

DIM Joy1 AS Joystick SHARED
CALL Joy1.Init($dc01)

DIM Joy2 AS Joystick SHARED
CALL Joy2.Init($dc00)
